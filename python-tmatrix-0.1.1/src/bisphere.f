C This test result was computed on an IBM RISC model 397 workstation
C for the current code setting.
C 
C R1= .5000D+01  R2= .5000D+01  R12= .2000D+02  LAM= .6283D+01
C X12= .2000D+02  NODRT= 33
C X1= .500000D+01 N1= .150000D+01 K1= .500000D-02 NODR(1)= 14
C X2= .500000D+01 N2= .150000D+01 K2= .500000D-02 NODR(2)= 14
C  CEXT= .589017D+03  CSCA= .567076D+03    W= .962749D+00  <COS>= .717082D+00
C  TEST OF VAN DER MEE & HOVENIER IS SATISFIED
C  
C   S      ALPHA1      ALPHA2      ALPHA3      ALPHA4       BETA1       BETA2
C   0     1.00000      .00000      .00000      .94160      .00000      .00000
C   1     2.15125      .00000      .00000     2.21375      .00000      .00000
C   2     2.99437     4.00361     3.83857     2.89376     -.13647      .11745
C   3     3.13877     3.55605     3.63214     3.20301     -.11917      .00216
C   4     3.32190     3.77498     3.68288     3.26877     -.15255      .10368
C   5     3.32936     3.46499     3.50235     3.40753     -.19897     -.04975
C   6     3.18348     3.58119     3.45692     3.09234     -.23832      .00832
C   7     2.95333     2.98997     3.11209     3.13450     -.27378     -.06001
C   8     2.47715     2.91924     2.73441     2.37083     -.43506     -.33890
C   9     1.94766     1.95517     1.99875     2.03174     -.08178     -.21840
C  10     1.62692     1.85138     1.74819     1.56046     -.15704     -.30807
C  11     1.20454     1.22355     1.22116     1.20713      .04472     -.06978
C  12     1.27983     1.27994     1.27321     1.27673      .01441     -.00550
C  13     1.39985     1.39649     1.39540     1.40073      .01718      .01608
C  14     1.52960     1.52807     1.52819     1.53191      .01704      .02120
C  15     1.66183     1.65843     1.65642     1.66209      .02479      .02294
C  16     1.79873     1.79692     1.79303     1.79602      .03754      .01614
C  17     1.88397     1.89037     1.88688     1.87990      .04223      .01045
C  18     1.87931     1.89487     1.89122     1.87306      .04009      .00650
C  19     1.77319     1.79585     1.79750     1.77103      .02763      .00827
C  20     1.59409     1.62019     1.62268     1.59283      .00972      .00493
C  21     1.37720     1.40242     1.40635     1.37793     -.00433      .00240
C  22     1.15582     1.17893     1.18291     1.15756     -.01639     -.00189
C  23      .93894      .95986      .96444      .94251     -.02933     -.00688
C  24      .73777      .75635      .75982      .74154     -.03810     -.01711
C  25      .55240      .56872      .57051      .55550     -.03998     -.02786
C  26      .38710      .40096      .40023      .38810     -.03397     -.03530
C  27      .24605      .25677      .25475      .24563     -.02365     -.03396
C  28      .14064      .14780      .14516      .13914     -.01241     -.02673
C  29      .06992      .07398      .07220      .06878     -.00530     -.01628
C  30      .03132      .03330      .03213      .03048     -.00145     -.00876
C  31      .01211      .01294      .01241      .01172     -.00016     -.00377
C  32      .00430      .00460      .00437      .00411      .00013     -.00148
C  33      .00133      .00143      .00135      .00127      .00011     -.00049
C  34      .00038      .00041      .00039      .00036      .00005     -.00015
C  35      .00010      .00011      .00010      .00009      .00002     -.00004
C  36      .00002      .00003      .00002      .00002      .00001     -.00001
C  37      .00001      .00001      .00001      .00000      .00000      .00000
C  
C       <       F11       F22       F33       F44       F12       F34
C     .00  49.78455  49.77951  49.77951  49.77447    .00000    .00000
C    5.00  35.98162  35.97666  35.97626  35.97159    .10705    .13306
C   10.00  16.24586  16.24110  16.23788  16.23416    .21533    .23828
C   15.00  10.67507  10.67056  10.65748  10.65496    .39163    .34560
C   20.00   8.90278   8.89855   8.85575   8.85437    .71344    .48493
C   25.00   4.67417   4.67021   4.59943   4.59892    .73578    .30617
C   30.00   1.90103   1.89726   1.82180   1.82187    .51177    .01957
C   35.00   1.11641   1.11257   1.03461   1.03518    .33577   -.19571
C   40.00   1.10229   1.09805   1.03145   1.03264    .15158   -.32446
C   45.00   1.13873   1.13393   1.09973   1.10167    .00530   -.26081
C   50.00   1.00942   1.00424    .99510    .99766   -.02457   -.11589
C   55.00    .85571    .85046    .84877    .85169    .02002   -.00392
C   60.00    .64929    .64408    .63356    .63674    .09647    .04628
C   65.00    .43570    .43039    .40340    .40693    .14346    .01536
C   70.00    .31993    .31440    .27708    .28099    .13643   -.04728
C   75.00    .28821    .28250    .24356    .24780    .10457   -.09298
C   80.00    .27305    .26716    .23409    .23865    .06754   -.10614
C   85.00    .22690    .22087    .19891    .20376    .03623   -.08551
C   90.00    .16608    .15991    .14797    .15309    .01953   -.05383
C   95.00    .13383    .12732    .12010    .12564    .01720   -.03445
C  100.00    .14155    .13466    .12760    .13359    .02517   -.02894
C  105.00    .16635    .15925    .14929    .15557    .04271   -.02521
C  110.00    .17408    .16672    .14829    .15490    .06852   -.01610
C  115.00    .14870    .14109    .10365    .11058    .09019   -.00940
C  120.00    .10957    .10169    .03337    .04060    .08931   -.02033
C  125.00    .09606    .08760   -.01761   -.00976    .05673   -.05878
C  130.00    .13177    .12253   -.01147   -.00280   -.00159   -.11762
C  135.00    .20600    .19550    .05854    .06853   -.06144   -.17075
C  140.00    .28155    .26925    .16422    .17607   -.08667   -.18663
C  145.00    .31664    .30306    .25096    .26414   -.05212   -.14696
C  150.00    .29863    .28522    .26592    .27896    .03527   -.06289
C  155.00    .25450    .24319    .18699    .19797    .13353    .03002
C  160.00    .23308    .22382    .03195    .04092    .19088    .09261
C  165.00    .27032    .26186   -.15254   -.14430    .17895    .10303
C  170.00    .35827    .34853   -.32017   -.31056    .10971    .07009
C  175.00    .45586    .43878   -.43657   -.41953    .03254    .02295
C  180.00    .50091    .47807   -.47807   -.45522    .00000    .00000

C   CALCULATION OF LIGHT SCATTERING BY RANDOMLY ORIENTED TWO-SPHERE
C   CLUSTERS WITH TOUCHING OR SEPARATED COMPONENTS
C                                                                    
C   Last update 04/15/2000.                                         
C                                                                  
C   The code has been developed by Michael Mishchenko at the NASA 
C   Goddard Institute for Space Studies and Daniel Mackowski
C   at Auburn University.
C                                                                     
C   The code can be used without any limitations in any not-for-      
C   profit scientific research.  The only request is that in any      
C   publication using the code the source of the code be acknowledged 
C   and relevant references be made.                                  
C                                                                     
C   The computational method is based on the superposition T-matrix 
C   approach and is described in the following papers: 
C
C     1. M. I. Mishchenko, Light scattering by randomly oriented
C        axially symmetric particles.  J. Opt. Soc. Am. A, vol. 8,
C        871-882 (1991).
C
C     2. D. W. Mackowski,Calculation of total cross sections of
C        multiple-sphere clusters.  J. Opt. Soc. Am. A, vol. 11,
C        2851-2861 (1994).
C 
C     3. M. I. Mishchenko and D. W. Mackowski, Light scattering by
C        randomly oriented bispheres.  Opt. Lett., vol. 19, 1604-
C        1606 (1994).
C     
C   Numerical tests and practical applications of the technique are
C   described in the papers
C
C     4. M. I. Mishchenko, D. W. Mackowski, and L. D. Travis, Scattering 
C        of light by bispheres with touching and separated components.  
C        Appl. Opt., vol. 34, 4589-4599 (1995).
C
C     5. M. I. Mishchenko and D. W. Mackowski, Electromagnetic scattreing
C        by randomly oriented bispheres:  comparison of theory and
C        experiment and benchmark calculations.  J. Quant. Spectrosc.
C        Radiat. Transfer, vol. 55, 683-694 (1996).
C
C     6. M. I. Mishchenko, Coherent backscattering by two-sphere
C        clusters.  Opt. Lett. 21, 623-625 (1996).
C
C   Analytical averaging over particle orientations (Ref. 1) makes   
C   this approach significantly faster than the standard technique based 
C   on numerical integrations over orientation angles. 
C   
C
C   INPUT:
C
C     LAM: wavelength of light
C     R, N, K: radius and real and imaginary parts of the 
C           refractive index for each sphere.  Note that
C           the component spheres may have different sizes and/or
C           refractive indices.
C     R12: distance between sphere centers.  
C          Touching spheres have R12=R(1)+R(2).
C     NODR1:  a convergence parameter
C         The number of multipole orders of each sphere are calculated
C         using the formula of Wiscombe  
C             NODR(I)=NINT(XI(I)+4.*XI(I)**(1./3.)+NODR1)
C         where XI(I) is the size parameter of sphere I.
C         Usually NODR1=2 provides reliable results.  However,
C         checking convergence over this parameter on a case-by-case
C         basis is a good idea.
C     NODRT1:  a convergence parameter
C         The number of multipole orders for T^0 matrix expansion
C         is calculated using the above formula for an equivalent 
C         sphere that completely encloses the two spheres.
C         usually NODRT1=2 gives good results, but occasional
C         checks of convergence over this parameter are recommended.
C     NPNA: the number of equidistant scattering angles (from 0  
C         to 180 deg) for which the scattering matrix is   
C         calculated.                                     
C
C
C   OUTPUT:
C
C     CEXT: extinction cross section     
C     CSCA: scattering cross section    
C     W: single-scattering albedo                   
C     <COS>: asymmetry parameter of the phase function 
C     ALPHA1,...,BETA2 - coefficients appearing in the expansions
C          of the elements of the scattering matrix in
C          generalized spherical functions
C          [Eqs. (2.25)-(2.30) of Ref. 1].
C     F11,...,F44: elements of the scattering matrix [as defined   
C            by equations (2.16)-(2.18) and (2.24) of Ref. 1]       
C            versus scattering angle                               
C
C   NOTES:
C
C     1) In order to avoid overfows, the size
C        of the array FAC in the subroutine BISPHERE
C        is limited to 165.  To make sure that the program
C        is not forced to go beyond the array boundary,
C        you should always use a compiler option which checks
C        each reference to an array element and stops the execution
C        if a reference goes out of bounds (e.g., the -qcheck option
C        on IBM UNIX workstations).
C        The code in its current setting prints out the 165th element
C        of the FAC array so that you could check whether the
C        code causes an overflow on your computer.
C
C     2) The T matrix T^0_{MNP MLQ} is calculated sequentially for
C        each azimuth mode M, with M=0,1,...NODR(1).  N and L are the
C        orders, and go from N,L=MIN(1,M)...NODRT.  P and Q 
C        take on values 1 or 2.
C
C     3) Important parameter variables are
C           NOD: maximum order of expansions for spheres.
C           NOTD: maximum order of T matrix expansion.
C        Larger particles may require larger values of these
C        parameters.  Note that NOD and NOTD should be changed
C        simultaneously in the main program and all subroutines.
C
C     4) The T matrix is expanded about the geometrical center
C        of the cluster, i.e., the center of the smallest sphere that
C        encloses the spheres.
C
C     5) If the spheres are of unequal size, the smaller sphere
C        (or, more exactly, the one needing fewer multipole orders)
C        must be number 2, and the larger number 1.
C
C     6) LAM, R, and R12 must be given in the same units of length,
C        and the dimension of CEXT and CSCA is that of LAM squared 
C        (e.g., if LAM is given in microns, then CEXT and CSCA      
C        are in square microns).   
C 
C     7) The physical correctness of the computed results is tested
C        using general inequalities derived by van der Mee and Hovenier, 
C        Astron. Astrophys., vol. 228, 559-568 (1990).  Although    
C        the message that the test of van der Mee and Hovenier is 
C        satisfied does not guarantee that the results are absolutely
C        correct, the message that the test is not satisfied can mean 
C        that something is wrong.      
C
C
C   We would highly appreciate informing us of any problems encountered
C   with this code.  Please send your message to the following         
C   e-mail address:  CRMIM@GISS.NASA.GOV.                              

C   WHILE THE COMPUTER PROGRAM HAS BEEN TESTED FOR A VARIETY OF CASES,
C   IT IS NOT INCONCEIVABLE THAT IT CONTAINS UNDETECTED ERRORS. ALSO,
C   INPUT PARAMETERS CAN BE USED WHICH ARE OUTSIDE THE ENVELOPE OF
C   VALUES FOR WHICH RESULTS ARE COMPUTED ACCURATELY. FOR THIS REASON,
C   THE AUTHORS AND THEIR ORGANIZATION DISCLAIM ALL LIABILITY FOR
C   ANY DAMAGES THAT MAY RESULT FROM THE USE OF THE PROGRAM. 

      SUBROUTINE tmbisphere(LAM,R1,N1,K1,R2,N2,K2,R12,
     &                      CEXT,CSCA,G,ERRCODE)

      IMPLICIT REAL*8 (A-H,O-Z)
      PARAMETER(NOD=60,NOD2=2*NOD,NAD=2*NOD2,NOTD=85,NOTD2=2*NOTD,
     &          NPN4=NOTD, NPN5=2*NPN4, NPN6=NPN4+1, NPL1=NPN5+1)
      REAL*8 ALPH1(NPL1),ALPH2(NPL1),ALPH3(NPL1),ALPH4(NPL1),
     &       BET1(NPL1),BET2(NPL1)
      REAL*8 R(2),XI(2),N(2),K(2),LAM,R1,N1,K1,R2,N2,K2,R12
      INTEGER ERRCODE

C      OPEN (1, FILE='bisphere.print')
C
C INPUT PARAMETERS
C
C      LAM=DACOS(-1D0)*2D0
      N(1)=N1  
      K(1)=K1
      N(2)=N2  
      K(2)=K2
      R(1)=R1
      R(2)=R2
C      R12=R(1)+R(2)
C      R12=R12
      NODR1=2
      NODRT1=2
      NPNA=37

      ERRCODE=0

C
C CALCULATE THE EXPANSION COEFFICIENTS
C
C      WRITE (1,9000) R(1),R(2),R12,LAM
C 9000 FORMAT ('R1=',D10.4,'  R2=',D10.4,'  R12=',D10.4,'  LAM=',D10.4)
      IF (R12.LT.R(1)+R(2)) THEN 
C         WRITE(*,'(''R12 IS SMALLER THAN R(1)+R(2)'')')      
C         STOP     
         ERRCODE=1
         RETURN     
      ENDIF
      IF (R(2).GT.R(1)) THEN 
C         WRITE(*,'(''R(2) IS GREATER THAN R(1)'')')      
C         STOP     
         ERRCODE=2
         RETURN     
      ENDIF
      PI=DACOS(-1D0)
      XI(1)=2D0*PI*R(1)/LAM
      XI(2)=2D0*PI*R(2)/LAM
      X12=2D0*PI*R12/LAM
      CALL BISPHERE (XI,X12,N,K,ALPH1,ALPH2,ALPH3,ALPH4,BET1,BET2,
     &               CEXT,CSCA,NODR1,NODRT1,L1MAX,LAM,ERRCODE)
      IF (ERRCODE.GT.0) RETURN
      WALB=CSCA/CEXT
      CALL HOVENR (L1MAX,ALPH1,ALPH2,ALPH3,ALPH4,BET1,BET2)
C      S=PI*R(1)*R(1)
C      QEXT=CEXT/S
C      QSCA=CSCA/S
C     WRITE (1,9100) QEXT,QSCA
C 9100 FORMAT ('QEXT=',D12.6,'  QSCA=',D12.6)
C
C CALCULATE THE SCATTERING MATRIX
C
      LMAX=L1MAX-1
      CALL MATR (ALPH1,ALPH2,ALPH3,ALPH4,BET1,BET2,LMAX,NPNA)
C      ITIME=MCLOCK()
C      TIME=DFLOAT(ITIME)/6000D0
C      PRINT 1001,TIME
C 1001 FORMAT (' TIME =',F8.2,' MIN')
C      STOP
      RETURN
      END

c*************************************************************

      SUBROUTINE BISPHERE (XI,X12,SN,SK,AL1,AL2,AL3,AL4,BE1,BE2,
     &                     CEXT,CSCA,NODR1,NODRT1,L1MAX,LAM,ERRCODE)
      IMPLICIT REAL*8 (A-H,O-Z)
      PARAMETER(NOD=60,NOD2=2*NOD,NAD=2*NOD2,NOTD=85,NOTD2=2*NOTD,
     &          NPN4=NOTD, NPN5=2*NPN4, NPN6=NPN4+1, NPL1=NPN5+1)

C  THESE ARRAYS ARE THE EXPANSION COEFFICIENTS

      REAL*8  AL1(NPL1),AL2(NPL1),AL3(NPL1),AL4(NPL1),
     &        BE1(NPL1),BE2(NPL1),LAM

C  THESE ARRAYS ARE THE ELEMENTS OF THE T-MATRIX

      REAL*4  RT11(NPN6,NPN4,NPN4),RT12(NPN6,NPN4,NPN4),
     &        RT21(NPN6,NPN4,NPN4),RT22(NPN6,NPN4,NPN4),
     &        IT11(NPN6,NPN4,NPN4),IT12(NPN6,NPN4,NPN4),
     &        IT21(NPN6,NPN4,NPN4),IT22(NPN6,NPN4,NPN4)
      INTEGER NODR(2)
      REAL*8 XI(2),CN1(NOD2,2),
     1       SN(2),SK(2),QE1(2),QS1(2),EN(NOTD),JM(NOTD,NOTD,2)
      COMPLEX*16 CI,A,AMT(NAD,2),
     1           AN1(NOD2,2),A1,B1,AM(NAD,NAD),AT(NOTD2,NOTD2),
     1           CMR12(0:NOD2),CNR12(0:NOD2),CN1R12(0:NOD2),
     1           CMRT(0:NOTD2),CNRT(0:NOTD2),CN1RT(0:NOTD2),
     1           CC(0:NOTD,0:NOTD)
      CHARACTER FOUT*30, FWRT*30

      INTEGER ERRCODE

C  THIS COMMON BLOCK IS USED TO TRANSFER THE T-MATRIX

      COMMON /TMAT/ RT11,RT12,RT21,RT22,IT11,IT12,IT21,IT22
      COMMON/FACTOR/FAC(0:165)
      DATA CI/(0D0,1D0)/
C
      PI=4.D0*DATAN(1.D0)
      FAC(0)=1.
      DO 5 N=1,165
         FAC(N)=DBLE(N)*FAC(N-1)
    5 CONTINUE
C      WRITE (6,1000) FAC(165)
C 1000 FORMAT ('FAC(165)=',D10.4)
C
      NOPT=0
      DO I=1,2
         NODR(I)=NINT(XI(I)+4.*XI(I)**(1./3.)+NODR1)
         IF(NODR(I).GT.NOD) THEN
C            WRITE(*,'(''NOD DIMENSION EXCEEDED!'')')      
C            STOP     
            ERRCODE=3
            RETURN
         ENDIF
C
C  MIE1 CALCULATES THE MIE MULTIPOLE COEFFICIENTS FOR THE SPHERES
C
         CALL MIE1(XI(I),SN(I),SK(I),NODR(I),QE1(I),
     1             QS1(I),AN1(1,I),CN1(1,I),0)
      ENDDO
      R12 = X12
      RT=R12*0.5D0
      REFF=R12
      NODRT=NINT(REFF+4.*REFF**(1./3.)+NODRT1)
C      WRITE (1,9500) X12,NODRT
C 9500 FORMAT ('X12=',D10.4,'  NODRT=',I3)
      IF(NODRT.GT.NOTD) THEN
C         WRITE(*,'('' NOTD DIMENSION EXCEEDED!'')')
C         STOP    
         ERRCODE=4
         RETURN
      ENDIF
      XV=(XI(1)**3+XI(2)**3)**(1./3.)
C
C HERE'S THE BEGINNING OF THE LOOP OVER DEGREE M
C
      QE=0.
      QS=0.
      CSCA=0D0
      CEXT=0D0
      DO M=0,NODR(1)
         M1=MAX(1,M)
         MO=M-1
C
C NBLK'S ARE THE DIMENSIONS OF THE MATRICIES
C
         NBLK1=2*(NODR(1)-M1+1)
         NBLK2=2*(NODR(2)-M1+1)
         NBLKT=2*(NODRT-M1+1)
         NEQNS=NBLK1+NBLK2
         DO N=1,NEQNS
            DO L=1,NEQNS
               AM(N,L)=0.
            ENDDO
         ENDDO
         DO N=M1,NODRT
            EN(N)=N*(N+1)/REAL(N+N+1)*FAC(N+M)/FAC(N-M)
         ENDDO
C
C CHECK IF M HAS EXCEEDED NODR(2) -- IF SO, THE ELEMENTS OF T WILL
C BE GIVEN DIRECTLY BY THE TRANSLATED MIE COEFFICIENTS.
C
         IF (M.GT.NODR(2)) GO TO 120
C
C  COMPUTE SCALAR ADDITION COEFFICIENTS FOR MA
C
         CALL VWHAC2(3,0,R12,NODR(2),NODR(1),M,MO,N,L,CMR12,
     1               CNR12,CN1R12,NOTD,CC,A1,B1)
C
C  COMPUTE A MATRIX
C
         DO N=M1,NODR(1)
            N1=N+N-1
            N2=N1+1
            I1=2*(N-M1)+1
            I2=I1+1
            AM(I1,I1)=1.
            AM(I2,I2)=1.
            DO L=M1,NODR(2)
               J1=2*(L-M1)+1+NBLK1
               J2=J1+1
               INL=(-1)**(N+L)
               CALL VWHAC2(3,1,R12,NODR(2),NODR(1),M,MO,L,N,
     1              CMR12,CNR12,CN1R12,NOTD,CC,A1,B1)
               AM(I1,J1)=INL*AN1(N1,1)*A1
               AM(I1,J2)=-INL*AN1(N1,1)*B1
               AM(I2,J1)=-INL*AN1(N2,1)*B1
               AM(I2,J2)=INL*AN1(N2,1)*A1
            ENDDO
         ENDDO
         DO N=M1,NODR(2)
            N1=N+N-1
            N2=N1+1
            I1=2*(N-M1)+1+NBLK1
            I2=I1+1
            AM(I1,I1)=1.
            AM(I2,I2)=1.
            DO L=M1,NODR(1)
               J1=2*(L-M1)+1
               J2=J1+1
               CALL VWHAC2(3,1,R12,NODR(2),NODR(1),M,MO,L,N,
     1              CMR12,CNR12,CN1R12,NOTD,CC,A1,B1)
               AM(I1,J1)=AN1(N1,2)*A1
               AM(I1,J2)=AN1(N1,2)*B1
               AM(I2,J1)=AN1(N2,2)*B1
               AM(I2,J2)=AN1(N2,2)*A1
            ENDDO
         ENDDO
C
C  INVERT
C
         CALL GJ2(AM,NBLK1,NEQNS,NAD)
C
C  MULT AM BY AN1
C
         DO J=1,NBLK1
            L=J+2*(M1-1)
            DO I=1,NEQNS
               AM(I,J)=AM(I,J)*AN1(L,1)
            ENDDO
         ENDDO
         DO J=1,NBLK2
            L=J+2*(M1-1)
            DO I=1,NEQNS
               AM(I,J+NBLK1)=AM(I,J+NBLK1)*AN1(L,2)
            ENDDO
         ENDDO
C
C  CALCULATE J ADDITION COEFFICIENTS - TRANSLATION OVER
C  DISTANCE OF RT
C
  120    MO=M-1
         CALL VWHAC2(3,0,RT,NODR(1),NODRT,M,MO,L,N,CMRT,CNRT,
     1              CN1RT,NOTD,CC,A1,B1)
         DO N=M1,NODR(1)
            DO L=M1,NODRT
               CALL VWHAC2(3,1,RT,NODR(1),NODRT,M,MO,L,N,
     1              CMRT,CNRT,CN1RT,NOTD,CC,A1,B1)
               JM(N,L,1)=DBLE(A1)
               JM(N,L,2)=-DBLE(CI*B1)
            ENDDO
         ENDDO
C
C  TRANSLATE THE ELEMTENTS OF THE T^{IJ} MATRIX TO THE CLUSTER
C  ORIGIN TO OBTAIN T^0
C
C
C  THIS BLOCK DOES TRANSLATION TO THE MIE COEFFICIENTS OF SPHERE 1
C  ONLY -- OCCURS FOR M > NODR(2)
C
         IF(M.GT.NODR(2)) THEN
            DO I=1,NBLKT
               N=(I-1)/2+M1
               DO J=1,NBLKT
                  L=(J-1)/2+M1
                  AT(I,J)=0.
                  IS=(-1)**(N+L)
                  DO K=1,NBLK1
                     NP=(K-1)/2+M1
                     IF(MOD(K+I,2).EQ.0) THEN
                        A1=JM(NP,N,1)*EN(NP)/EN(N)
                     ELSE
                        A1=CI*JM(NP,N,2)*EN(NP)/EN(N)
                     ENDIF
                     IF(MOD(K+J,2).EQ.0) THEN
                        A1=A1*JM(NP,L,1)
                     ELSE
                        A1=-A1*CI*JM(NP,L,2)
                     ENDIF
                     AT(I,J)=AT(I,J)+AN1(K+2*(M1-1),1)*A1*IS
                  ENDDO
               ENDDO
            ENDDO
            GOTO 200
         ENDIF
C
C THIS BLOCK TRANSLATES THE FULL T-MATRIX
C
         DO I=1,NEQNS
            DO J=1,NBLK1
               AMT(J,1)=AM(I,J)
               AMT(J,2)=0.
            ENDDO
            DO J=1,NBLK2
               AMT(J,2)=AM(I,J+NBLK1)
            ENDDO
            DO J=1,NBLKT
               AM(I,J)=0.
               L=(J-1)/2+M1
               DO K=1,NBLK1
                  LP=(K-1)/2+M1
                  IS=(-1)**(L+LP)
                  IF(MOD(K+J,2).EQ.0) THEN
                     A=JM(LP,L,1)
                  ELSE
                     A=CI*JM(LP,L,2)
                     IS=-IS
                  ENDIF
                  AM(I,J)=AM(I,J)+(AMT(K,1)*IS+AMT(K,2))*A
               ENDDO
            ENDDO
         ENDDO
         DO J=1,NBLKT
            DO I=1,NBLK1
               AMT(I,1)=AM(I,J)
               AMT(I,2)=0.
            ENDDO
            DO I=1,NBLK2
               AMT(I,2)=AM(I+NBLK1,J)
            ENDDO
            DO I=1,NBLKT
               AT(I,J)=0.
               N=(I-1)/2+M1
               DO K=1,NBLK1
                  NP=(K-1)/2+M1
                  IS=(-1)**(N+NP)
                  IF(MOD(K+I,2).EQ.0) THEN
                     A=JM(NP,N,1)*EN(NP)/EN(N)
                  ELSE
                     A=-CI*JM(NP,N,2)*EN(NP)/EN(N)
                     IS=-IS
                  ENDIF
                  AT(I,J)=AT(I,J)+(AMT(K,1)*IS+AMT(K,2))*A
               ENDDO
            ENDDO
         ENDDO
C
C  COMPUTE RANDOM-ORIENTATION EXTINCTION, SCATTERING
C
  200    QEM=0.
         QSM=0.
         DO N=M1,NODRT
            I1=2*(N-M1)+1
            I2=I1+1
            N1=N+N-1
            N2=N1+1
            QEM=QEM+AT(I1,I1)+AT(I2,I2)
            DO L=M1,NODRT
               J1=2*(L-M1)+1
               J2=J1+1
               QSM=QSM+EN(N)*(AT(I1,J1)*DCONJG(AT(I1,J1))
     1                 +AT(I1,J2)*DCONJG(AT(I1,J2)))/EN(L)
               QSM=QSM+EN(N)*(AT(I2,J1)*DCONJG(AT(I2,J1))
     1                 +AT(I2,J2)*DCONJG(AT(I2,J2)))/EN(L)
            ENDDO
         ENDDO
         IF(M.EQ.0) THEN
            QE=QE+QEM
            QS=QS+QSM
         ELSE
            QE=QE+2.*QEM
            QS=QS+2.*QSM
         ENDIF
         NM=NODRT-M1+1
         MIND=M+1
         DO 214 N2=1,NM
            NN2=N2+M1-1
            N22=(N2-1)*2+1
            DO 214 N1=1,NM
               NN1=N1+M1-1
               N11=(N1-1)*2+1
               EPSC=-DSQRT(EN(NN1)/EN(NN2))
               ZZ1=AT(N11+1,N22+1)*EPSC
               RT11(MIND,NN1,NN2)=ZZ1
               ZZ2=AT(N11+1,N22+1)*(-CI)*EPSC
               IT11(MIND,NN1,NN2)=ZZ2
               ZZ3=AT(N11+1,N22)*EPSC
               RT12(MIND,NN1,NN2)=ZZ3
               ZZ4=AT(N11+1,N22)*(-ci)*EPSC
               IT12(MIND,NN1,NN2)=ZZ4
               ZZ5=AT(N11,N22+1)*EPSC
               RT21(MIND,NN1,NN2)=ZZ5
               ZZ6=AT(N11,N22+1)*(-CI)*EPSC
               IT21(MIND,NN1,NN2)=ZZ6
               ZZ7=AT(N11,N22)*EPSC
               RT22(MIND,NN1,NN2)=ZZ7
               ZZ8=AT(N11,N22)*(-CI)*EPSC
               IT22(MIND,NN1,NN2)=ZZ8
               IF (M.EQ.0) THEN
                  CSCA=CSCA+(ZZ1*ZZ1+ZZ2*ZZ2+ZZ3*ZZ3+ZZ4*ZZ4
     &                       +ZZ5*ZZ5+ZZ6*ZZ6+ZZ7*ZZ7+ZZ8*ZZ8)
                ELSE
                  CSCA=CSCA+(ZZ1*ZZ1+ZZ2*ZZ2+ZZ3*ZZ3+ZZ4*ZZ4
     &                       +ZZ5*ZZ5+ZZ6*ZZ6+ZZ7*ZZ7+ZZ8*ZZ8)*2D0
               ENDIF
  214    CONTINUE
         DO 215 N=M1,NODRT  
            IF (M.EQ.0) THEN
               CEXT=CEXT+RT11(MIND,N,N)+RT22(MIND,N,N)
             ELSE
               CEXT=CEXT+2d0*(RT11(MIND,N,N)+RT22(MIND,N,N))
            ENDIF
  215    CONTINUE
C
C LOOP ON TO THE NEXT DEGREE M
C
      ENDDO
C
C CALCULATE THE EXPANSION COEFFICIENTS
C
C      WRITE (1,9000) XI(1),SN(1),SK(1),NODR(1)
C      WRITE (1,9001) XI(2),SN(2),SK(2),NODR(2)
C 9000 FORMAT('X1=',D12.6,' N1=',D12.6,' K1=',D12.6,' NODR(1)=',I3)
C 9001 format('X2=',D12.6,' N2=',D12.6,' K2=',D12.6,' NODR(2)=',I3)
      DLAM=2d0*PI
      COEFF1=DLAM*DLAM*0.5D0/PI
      CSCA=CSCA*COEFF1
      CEXT=-CEXT*COEFF1
      CALL GSP (NODRT,NODR(1),CSCA,DLAM,AL1,AL2,AL3,AL4,BE1,BE2,LMAX,
     &          ERRCODE)
      IF (ERRCODE.GT.0) RETURN
      L1MAX=LMAX+1
      COEFF1=LAM*LAM*0.25D0/(PI*PI)
      CSCA=CSCA*COEFF1
      CEXT=CEXT*COEFF1
      WALB=CSCA/CEXT
      ASYMM=AL1(2)/3D0
C      write (1,9100) CEXT,CSCA,WALB,ASYMM
      XI1=XI(1)
      XI2=XI(2)
      REV=(XI1*XI1*XI1+XI2*XI2*XI2)**(1D0/3D0)
      COEFF1=1D0/(PI*REV*REV)
      QSCA=CSCA*COEFF1
      QEXT=CEXT*COEFF1
      QABS=QEXT-QSCA
C     WRITE (1,9200) QEXT,QSCA,QABS
C 9100 FORMAT(' CEXT=',D12.6,2X,'CSCA=',D12.6,2X,  
C     &       2X,'W=',D12.6,2X,'<COS>=',D12.6)
C 9200 FORMAT(' QEXT=',D12.6,2X,'QSCA=',D12.6,2X,'QABS=',D22.16)
      RETURN
      END

C***************************************************************
 
C    CALCULATION OF THE SCATTERING MATRIX FOR GIVEN EXPANSION
C    COEFFICIENTS
 
C    A1,...,B2 - EXPANSION COEFFICIENTS
C    LMAX - NUMBER OF COEFFICIENTS MINUS 1
C    NPNA - NUMBER OF SCATTERING ANGLES
C        THE CORRESPONDING SCATTERING ANGLES ARE GIVEN BY
C        180*(I-1)/(NPNA-1) (DEGREES), WHERE I NUMBERS THE ANGLES
 
      SUBROUTINE MATR(A1,A2,A3,A4,B1,B2,LMAX,NPNA)
      PARAMETER (NOTD=85, NPL1=2*NOTD+1) 
      IMPLICIT REAL*8 (A-H,O-Z)
      REAL*8 A1(NPL1),A2(NPL1),A3(NPL1),A4(NPL1),B1(NPL1),B2(NPL1)
      N=NPNA 
      DN=1D0/DFLOAT(N-1)
      DA=DACOS(-1D0)*DN
      DB=180D0*DN
      L1MAX=LMAX+1
C      WRITE (1,1000)
C 1000 FORMAT(' ')
C      WRITE (1,1001)
C 1001 FORMAT(2X,'S',6X,'ALPHA1',6X,'ALPHA2',6X,'ALPHA3',
C     &       6X,'ALPHA4',7X,'BETA1',7X,'BETA2')
C      DO 1 L=1,L1MAX
C         WRITE (1,1002) L-1,A1(L),A2(L),A3(L),A4(L),B1(L),B2(l)
C    1 CONTINUE
C      WRITE (1,1000)
C 1002 FORMAT (I3,6F12.5)
      TB=-DB
      TAA=-DA
      D6=DSQRT(6D0)*0.25D0
C      WRITE (1,1003)
C 1003 FORMAT(' ',5X,'<',7X,'F11',7X,'F22',7X,'F33',
C     & 7X,'F44',7X,'F12',7X,'F34')
      DO 500 I1=1,N
         TAA=TAA+DA
         TB=TB+DB
         U=DCOS(TAA)
         F11=0D0
         F2=0D0
         F3=0D0
         F44=0D0
         F12=0D0
         F34=0D0
         P1=0D0
         P2=0D0
         P3=0D0
         P4=0D0
         PP1=1D0
         PP2=0.25D0*(1D0+U)*(1D0+U)
         PP3=0.25D0*(1D0-U)*(1D0-U)
         PP4=D6*(U*U-1D0)
         DO 400 L1=1,L1MAX
            L=L1-1
            DL=DFLOAT(L)
            DL1=DFLOAT(L1)
            F11=F11+A1(L1)*PP1
            F44=F44+A4(L1)*PP1
            IF(L.EQ.LMAX) GO TO 350
            PL1=DFLOAT(2*L+1)
            P=(PL1*U*PP1-DL*P1)/DL1
            P1=PP1
            PP1=P
  350       IF(L.LT.2) GO TO 400
            F2=F2+(A2(L1)+A3(L1))*PP2
            F3=F3+(A2(L1)-A3(L1))*PP3
            F12=F12+B1(L1)*PP4
            F34=F34+B2(L1)*PP4
            IF(L.EQ.LMAX) GO TO 400
            PL2=DFLOAT(L*L1)*U
            PL3=DFLOAT(L1*(L*L-4))
            PL4=1D0/DFLOAT(L*(L1*L1-4))
            P=(PL1*(PL2-4D0)*PP2-PL3*P2)*PL4
            P2=PP2
            PP2=P
            P=(PL1*(PL2+4D0)*PP3-PL3*P3)*PL4
            P3=PP3
            PP3=P
            P=(PL1*U*PP4-DSQRT(DFLOAT(L*L-4))*P4)/DSQRT(DFLOAT(L1*L1-4))
            P4=PP4
            PP4=P
  400    CONTINUE
         F22=(F2+F3)*0.5D0
         F33=(F2-F3)*0.5D0
C        F22=100D0*F22/F11
C        F33=100D0*F33/F11
C        F44=100D0*F44/F11
C        F12=-100D0*F12/F11
C        F34=100D0*F34/F11
C         WRITE (1,1004) TB,F11,F22,F33,F44,F12,F34
  500 CONTINUE
C 1004 FORMAT(' ',F6.2,6F10.5)
      RETURN
      END

c****************************************************************

      SUBROUTINE HOVENR (L1,A1,A2,A3,A4,B1,B2)
      IMPLICIT REAL*8 (A-H,O-Z)
      REAL*8 A1(L1),A2(L1),A3(L1),A4(L1),B1(L1),B2(L1)
      DO 100 L=1,L1
         KONTR=1
         LL=L-1
         DL=DFLOAT(LL)*2D0+1D0
         DDL=DL*0.48D0
         AA1=A1(L)
         AA2=A2(L)
         AA3=A3(L)
         AA4=A4(L)
         BB1=B1(L)
         BB2=B2(L)
         IF(LL.GE.1.AND.DABS(AA1).GE.DL) KONTR=2
         IF(DABS(AA2).GE.DL) KONTR=2
         IF(DABS(AA3).GE.DL) KONTR=2
         IF(DABS(AA4).GE.DL) KONTR=2
         IF(DABS(BB1).GE.DDL) KONTR=2
         IF(DABS(BB2).GE.DDL) KONTR=2
C         IF(KONTR.EQ.2) WRITE (1,3000) LL
         C=-0.1D0
         DO 50 I=1,11
            C=C+0.1D0
            CC=C*C
            C1=CC*BB2*BB2
            C2=C*AA4
            C3=C*AA3
            IF((DL-C*AA1)*(DL-C*AA2)-CC*BB1*BB1.LE.-1D-6) KONTR=2
            IF((DL-C2)*(DL-C3)+C1.LE.-1D-6) KONTR=2
            IF((DL+C2)*(DL-C3)-C1.LE.-1D-6) KONTR=2
            IF((DL-C2)*(DL+C3)-C1.LE.-1D-6) KONTR=2
C            IF(KONTR.EQ.2) WRITE (1,4000) LL,C
   50    CONTINUE
  100 CONTINUE
C      IF(KONTR.EQ.1) WRITE (1,2000)   
C 2000 FORMAT(' TEST OF VAN DER MEE & HOVENIER IS SATISFIED')
C 3000 FORMAT(' TEST OF VAN DER MEE & HOVENIER IS NOT SATISFIED, L=',I3)
C 4000 FORMAT(' TEST OF VAN DER MEE & HOVENIER IS NOT SATISFIED, L=',I3,
C     & 'A=',D9.2)
      RETURN
      end
 
C********************************************************************
C                                                                   *
C   CALCULATION OF THE EXPANSION COEFFICIENTS FOR (I,Q,U,V) -       *
C   REPRESENTATION.                                                 *
C                                                                   *
C   INPUT PARAMETERS:                                               *
C                                                                   *
C      LAM - WAVELENGTH OF LIGHT                                    *
C      CSCA - SCATTERING CROSS SECTION                              *
C      TR AND TI - ELEMENTS OF THE T-MATRIX. TRANSFERRED THROUGH    *
C                  COMMON /TMAT/                                    *
C      NMAX - DIMENSION OF T(M)-MATRICES                            *
C      MORD - MAXIMUM M-NUMBER                                      *
C                                                                   *
C   OUTPUT INFORTMATION:                                            *
C                                                                   *
C      ALF1,...,ALF4,BET1,BET2 - EXPANSION COEFFICIENTS             *
C      LMAX - NUMBER OF COEFFICIENTS MINUS 1                        *
C                                                                   *
C********************************************************************
 
      SUBROUTINE GSP(NMAX,MORD,CSCA,LAM,
     &               ALF1,ALF2,ALF3,ALF4,BET1,BET2,LMAX,ERRCODE)
      PARAMETER (NOTD=85, NPN4=NOTD, NPN5=2*NPN4, 
     &           NPN6=NPN4+1, NPL1=NPN5+1)
      IMPLICIT REAL*8 (A-B,D-H,O-Z),COMPLEX*16 (C)
      REAL*8 SSIGN(600),F(500)
      REAL*8 LAM
      REAL*8  CSCA,SSI(NPL1),SSJ(NPN4),
     &        ALF1(NPL1),ALF2(NPL1),ALF3(NPL1),
     &        ALF4(NPL1),BET1(NPL1),BET2(NPL1),
     &        TR1(NPL1,NPN4),TR2(NPL1,NPN4),
     &        TI1(NPL1,NPN4),TI2(NPL1,NPN4),
     &        G1(NPL1,NPN6),G2(NPL1,NPN6),
     &        AR1(NPN4),AR2(NPN4),AI1(NPN4),AI2(NPN4),
     &        FR(NPN4,NPN4),FI(NPN4,NPN4),FF(NPN4,NPN4)
      REAL*4 B1R(NPL1,NPL1,NPN4),B1I(NPL1,NPL1,NPN4),
     &       B2R(NPL1,NPL1,NPN4),B2I(NPL1,NPL1,NPN4),
     &       D1(NPL1,NPN4,NPN4),D2(NPL1,NPN4,NPN4),
     &       D3(NPL1,NPN4,NPN4),D4(NPL1,NPN4,NPN4),
     &       D5R(NPL1,NPN4,NPN4),D5I(NPL1,NPN4,NPN4)
      REAL*4
     &     TR11(NPN6,NPN4,NPN4),TR12(NPN6,NPN4,NPN4),
     &     TR21(NPN6,NPN4,NPN4),TR22(NPN6,NPN4,NPN4),
     &     TI11(NPN6,NPN4,NPN4),TI12(NPN6,NPN4,NPN4),
     &     TI21(NPN6,NPN4,NPN4),TI22(NPN6,NPN4,NPN4)
      COMPLEX*16 CIM(NPN4)
      INTEGER ERRCODE
 
      COMMON /TMAT/ TR11,TR12,TR21,TR22,TI11,TI12,TI21,TI22
      COMMON /SS/ SSIGN
      COMMON /FAC/ F
 
      CALL FACT
      CALL SIGNUM
      LMAX=2*NMAX
      L1MAX=LMAX+1
      CI=(0D0,1D0)
      CIM(1)=CI
      DO 2 I=2,NMAX
         CIM(I)=CIM(I-1)*CI
    2 CONTINUE
      SSI(1)=1D0
      DO 3 I=1,LMAX
         I1=I+1
         SI=DFLOAT(2*I+1)
         SSI(I1)=SI
         IF(I.LE.NMAX) SSJ(I)=DSQRT(SI)
    3 CONTINUE
      CI=-CI
      DO 5 I=1,NMAX
         SI=SSJ(I)
         CCI=CIM(I)
         DO 4 J=1,NMAX
            SJ=1D0/SSJ(J)
            CCJ=CIM(J)*SJ/CCI
            FR(J,I)=CCJ
            FI(J,I)=CCJ*CI
            FF(J,I)=SI*SJ
    4    CONTINUE
    5 CONTINUE
      NMAX1=NMAX+1
 
C *****  CALCULATION OF THE ARRAYS B1 AND B2  *****
 
      K1=1
      K2=0
      K3=0
      K4=1
      K5=1
      K6=2

      DO 100 N=1,NMAX
 
C *****  CALCULATION OF THE ARRAYS T1 AND T2  *****
 
         DO 10 NN=1,NMAX
            M1MAX=MIN0(N,NN,MORD)+1
            DO 6 M1=1,M1MAX
               M=M1-1
               L1=NPN6+M
               TT1=TR11(M1,N,NN)
               TT2=TR12(M1,N,NN)
               TT3=TR21(M1,N,NN)
               TT4=TR22(M1,N,NN)
               TT5=TI11(M1,N,NN)
               TT6=TI12(M1,N,NN)
               TT7=TI21(M1,N,NN)
               TT8=TI22(M1,N,NN)
               T1=TT1+TT2
               T2=TT3+TT4
               T3=TT5+TT6
               T4=TT7+TT8
               TR1(L1,NN)=T1+T2
               TR2(L1,NN)=T1-T2
               TI1(L1,NN)=T3+T4
               TI2(L1,NN)=T3-T4
               IF(M.EQ.0) GO TO 6
               L1=NPN6-M
               T1=TT1-TT2
               T2=TT3-TT4
               T3=TT5-TT6
               T4=TT7-TT8
               TR1(L1,NN)=T1-T2
               TR2(L1,NN)=T1+T2
               TI1(L1,NN)=T3-T4
               TI2(L1,NN)=T3+T4
    6       CONTINUE
   10    CONTINUE
 
C  *****  END OF THE CALCULATION OF THE ARRAYS T1 AND T2  *****
 
         NN1MAX=NMAX1+N
         DO 40 NN1=1,NN1MAX
            N1=NN1-1
 
C  *****  CALCULATION OF THE ARRAYS A1 AND A2  *****
 
            CALL CCG(N,N1,NMAX,K1,K2,G1,ERRCODE)
            IF (ERRCODE.GT.0) RETURN
            NNMAX=MIN0(NMAX,N1+N)
            NNMIN=MAX0(1,IABS(N-N1))
            KN=N+NN1
            DO 15 NN=NNMIN,NNMAX
               NNN=NN+1
               SIG=SSIGN(KN+NN)
               M1MAX=MIN0(N,NN,MORD)+NPN6
               AAR1=0D0
               AAR2=0D0
               AAI1=0D0
               AAI2=0D0
               DO 13 M1=NPN6,M1MAX
                  M=M1-NPN6
                  SSS=G1(M1,NNN)
                  RR1=TR1(M1,NN)
                  RI1=TI1(M1,NN)
                  RR2=TR2(M1,NN)
                  RI2=TI2(M1,NN)
                  IF(M.EQ.0) GO TO 12
                  M2=NPN6-M
                  RR1=RR1+TR1(M2,NN)*SIG
                  RI1=RI1+TI1(M2,NN)*SIG
                  RR2=RR2+TR2(M2,NN)*SIG
                  RI2=RI2+TI2(M2,NN)*SIG
   12             AAR1=AAR1+SSS*RR1
                  AAI1=AAI1+SSS*RI1
                  AAR2=AAR2+SSS*RR2
                  AAI2=AAI2+SSS*RI2
   13          CONTINUE
               XR=FR(NN,N)
               XI=FI(NN,N)
               AR1(NN)=AAR1*XR-AAI1*XI
               AI1(NN)=AAR1*XI+AAI1*XR
               AR2(NN)=AAR2*XR-AAI2*XI
               AI2(NN)=AAR2*XI+AAI2*XR
   15       CONTINUE
 
C  *****  END OF THE CALCULATION OF THE ARRAYS A1 AND A2 ****
 
            CALL CCG(N,N1,NMAX,K3,K4,G2,ERRCODE)
            IF (ERRCODE.GT.0) RETURN
            M1=MAX0(-N1+1,-N)
            M2=MIN0(N1+1,N)
            M1MAX=M2+NPN6
            M1MIN=M1+NPN6
            DO 30 M1=M1MIN,M1MAX
               BBR1=0D0
               BBI1=0D0
               BBR2=0D0
               BBI2=0D0
               DO 25 NN=NNMIN,NNMAX
                  NNN=NN+1
                  SSS=G2(M1,NNN)
                  BBR1=BBR1+SSS*AR1(NN)
                  BBI1=BBI1+SSS*AI1(NN)
                  BBR2=BBR2+SSS*AR2(NN)
                  BBI2=BBI2+SSS*AI2(NN)
   25          CONTINUE
               B1R(NN1,M1,N)=BBR1
               B1I(NN1,M1,N)=BBI1
               B2R(NN1,M1,N)=BBR2
               B2I(NN1,M1,N)=BBI2
   30       CONTINUE
   40    CONTINUE
  100 CONTINUE
 
C  *****  END OF THE CALCULATION OF THE ARRAYS B1 AND B2 ****
 
C  *****  CALCULATION OF THE ARRAYS D1,D2,D3,D4, AND D5  *****
 
      DO 200 N=1,NMAX
         DO 190 NN=1,NMAX
            M1=MIN0(N,NN)
            M1MAX=NPN6+M1
            M1MIN=NPN6-M1
            NN1MAX=NMAX1+MIN0(N,NN)
            DO 180 M1=M1MIN,M1MAX
               M=M1-NPN6
               NN1MIN=IABS(M-1)+1
               DD1=0D0
               DD2=0D0
               DO 150 NN1=NN1MIN,NN1MAX
                  XX=SSI(NN1)
                  X1=B1R(NN1,M1,N)
                  X2=B1I(NN1,M1,N)
                  X3=B1R(NN1,M1,NN)
                  X4=B1I(NN1,M1,NN)
                  X5=B2R(NN1,M1,N)
                  X6=B2I(NN1,M1,N)
                  X7=B2R(NN1,M1,NN)
                  X8=B2I(NN1,M1,NN)
                  DD1=DD1+XX*(X1*X3+X2*X4)
                  DD2=DD2+XX*(X5*X7+X6*X8)
  150          CONTINUE
               D1(M1,NN,N)=DD1
               D2(M1,NN,N)=DD2
  180       CONTINUE
            MMAX=MIN0(N,NN+2)
            MMIN=MAX0(-N,-NN+2)
            M1MAX=NPN6+MMAX
            M1MIN=NPN6+MMIN
            DO 186 M1=M1MIN,M1MAX
               M=M1-NPN6
               NN1MIN=IABS(M-1)+1
               DD3=0D0
               DD4=0D0
               DD5R=0D0
               DD5I=0D0
               M2=-M+2+NPN6
               DO 183 NN1=NN1MIN,NN1MAX
                  XX=SSI(NN1)
                  X1=B1R(NN1,M1,N)
                  X2=B1I(NN1,M1,N)
                  X3=B2R(NN1,M1,N)
                  X4=B2I(NN1,M1,N)
                  X5=B1R(NN1,M2,NN)
                  X6=B1I(NN1,M2,NN)
                  X7=B2R(NN1,M2,NN)
                  X8=B2I(NN1,M2,NN)
                  DD3=DD3+XX*(X1*X5+X2*X6)
                  DD4=DD4+XX*(X3*X7+X4*X8)
                  DD5R=DD5R+XX*(X3*X5+X4*X6)
                  DD5I=DD5I+XX*(X4*X5-X3*X6)
  183          CONTINUE
               D3(M1,NN,N)=DD3
               D4(M1,NN,N)=DD4
               D5R(M1,NN,N)=DD5R
               D5I(M1,NN,N)=DD5I
  186       CONTINUE
  190    CONTINUE
  200 CONTINUE
 
C  *****  END OF THE CALCULATION OF THE D-ARRAYS *****
 
C  *****  CALCULATION OF THE EXPANSION COEFFICIENTS *****
 
      DK=LAM*LAM/(4D0*CSCA*DACOS(-1D0))
      DO 300 L1=1,L1MAX
         G1L=0D0
         G2L=0D0
         G3L=0D0
         G4L=0D0
         G5LR=0D0
         G5LI=0D0
         L=L1-1
         SL=SSI(L1)*DK
         DO 290 N=1,NMAX
            NNMIN=MAX0(1,IABS(N-L))
            NNMAX=MIN0(NMAX,N+L)
            IF(NNMAX.LT.NNMIN) GO TO 290
            CALL CCG(N,L,NMAX,K1,K2,G1,ERRCODE)
            IF (ERRCODE.GT.0) RETURN
            IF(L.GE.2) CALL CCG(N,L,NMAX,K5,K6,G2,ERRCODE)
            IF (ERRCODE.GT.0) RETURN
            NL=N+L
            DO 280  NN=NNMIN,NNMAX
               NNN=NN+1
               MMAX=MIN0(N,NN)
               M1MIN=NPN6-MMAX
               M1MAX=NPN6+MMAX
               SI=SSIGN(NL+NNN)
               DM1=0D0
               DM2=0D0
               DO 270 M1=M1MIN,M1MAX
                  M=M1-NPN6
                  IF(M.GE.0) SSS1=G1(M1,NNN)
                  IF(M.LT.0) SSS1=G1(NPN6-M,NNN)*SI
                  DM1=DM1+SSS1*D1(M1,NN,N)
                  DM2=DM2+SSS1*D2(M1,NN,N)
  270          CONTINUE
               FFN=FF(NN,N)
               SSS=G1(NPN6+1,NNN)*FFN
               G1L=G1L+SSS*DM1
               G2L=G2L+SSS*DM2*SI
               IF(L.LT.2) GO TO 280
               DM3=0D0
               DM4=0D0
               DM5R=0D0
               DM5I=0D0
               MMAX=MIN0(N,NN+2)
               MMIN=MAX0(-N,-NN+2)
               M1MAX=NPN6+MMAX
               M1MIN=NPN6+MMIN
               DO 275 M1=M1MIN,M1MAX
                  M=M1-NPN6
                  SSS1=G2(NPN6-M,NNN)
                  DM3=DM3+SSS1*D3(M1,NN,N)
                  DM4=DM4+SSS1*D4(M1,NN,N)
                  DM5R=DM5R+SSS1*D5R(M1,NN,N)
                  DM5I=DM5I+SSS1*D5I(M1,NN,N)
  275          CONTINUE
               G5LR=G5LR-SSS*DM5R
               G5LI=G5LI-SSS*DM5I
               SSS=G2(NPN4,NNN)*FFN
               G3L=G3L+SSS*DM3
               G4L=G4L+SSS*DM4*SI
  280       CONTINUE
  290    CONTINUE
         G1L=G1L*SL
         G2L=G2L*SL
         G3L=G3L*SL
         G4L=G4L*SL
         G5LR=G5LR*SL
         G5LI=G5LI*SL
         ALF1(L1)=G1L+G2L
         ALF2(L1)=G3L+G4L
         ALF3(L1)=G3L-G4L
         ALF4(L1)=G1L-G2L
         BET1(L1)=G5LR*2D0
         BET2(L1)=G5LI*2D0
         LMAX=L
         IF(DABS(G1L).LT.1D-5) GO TO 500
  300 CONTINUE
  500 CONTINUE
      RETURN
      END
 
C****************************************************************
 
C   CALCULATION OF THE QUANTITIES F(N+1)=0.5*LN(N!)
C   0.LE.N.LE.499
 
      SUBROUTINE FACT
      REAL*8 F(500)
      COMMON /FAC/ F
      F(1)=0D0
      F(2)=0D0
      DO 2 I=3,500
         I1=I-1
         F(I)=F(I1)+0.5D0*DLOG(DFLOAT(I1))
    2 CONTINUE
      RETURN
      END
 
C************************************************************
 
C   CALCULATION OF THE ARRAY SSIGN(N+1)=SIGN(N)
C   0.LE.N.LE.599
 
      SUBROUTINE SIGNUM
      REAL*8 SSIGN(600)
      COMMON /SS/ SSIGN
      SSIGN(1)=1D0
      DO 2 N=2,600
         SSIGN(N)=-SSIGN(N-1)
    2 CONTINUE
      RETURN
      END

C********************************************************************
C
C   CALCULATION OF CLEBSCH-GORDAN COEFFICIENTS
C   (N,M:N1,M1/NN,MM)
C   FOR GIVEN N AND N1. M1=MM-M, INDEX MM IS FOUND FROM M AS
C   MM=M*K1+K2
C
C   INPUT PARAMETERS :  N,N1,NMAX,K1,K2
C                               N.LE.NMAX
C                               N.GE.1
C                               N1.GE.0
C                               N1.LE.N+NMAX
C   OUTPUT PARAMETERS : GG(M+NPN6,NN+1) - ARRAY OF THE CORRESPONDING
C                                       COEFFICIENTS
C                               /M/.LE.N
C                               /M1/=/M*(K1-1)+K2/.LE.N1
C                               NN.LE.MIN(N+N1,NMAX)
C                               NN.GE.MAX(/MM/,/N-N1/)
C   IF K1=1 AND K2=0, THEN 0.LE.M.LE.N
 
      SUBROUTINE CCG(N,N1,NMAX,K1,K2,GG,ERRCODE)
      PARAMETER (NOTD=85, NPN4=NOTD, NPN5=2*NPN4, 
     &           NPN6=NPN4+1, NPL1=NPN5+1)
      IMPLICIT REAL*8 (A-H,O-Z)
      REAL*8 GG(NPL1,NPN6),CD(0:NPN5),CU(0:NPN5)
      INTEGER ERRCODE

      IF(NMAX.LE.NPN4.
     &   AND.0.LE.N1.
     &   AND.N1.LE.NMAX+N.
     &   AND.N.GE.1.
     &   AND.N.LE.NMAX) GO TO 1
C      PRINT 5001
C      STOP
C 5001 FORMAT(' ERROR IN SUBROUTINE CCG')
      ERRCODE=5
      RETURN
    1 NNF=MIN0(N+N1,NMAX)
      MIN=NPN6-N
      MF=NPN6+N
      IF(K1.EQ.1.AND.K2.EQ.0) MIN=NPN6
      DO 100 MIND=MIN,MF
         M=MIND-NPN6
         MM=M*K1+K2
         M1=MM-M
         IF(IABS(M1).GT.N1) GO TO 90
         NNL=MAX0(IABS(MM),IABS(N-N1))
         IF(NNL.GT.NNF) GO TO 90
         NNU=N+N1
         NNM=(NNU+NNL)*0.5D0
         IF (NNU.EQ.NNL) NNM=NNL
         CALL CCGIN(N,N1,M,MM,C,ERRCODE)
         IF (ERRCODE.GT.0) RETURN
         CU(NNL)=C  
         IF (NNL.EQ.NNF) GO TO 50
         C2=0D0
         C1=C
         DO 7 NN=NNL+1,MIN0(NNM,NNF)
            A=DFLOAT((NN+MM)*(NN-MM)*(N1-N+NN))
            A=A*DFLOAT((N-N1+NN)*(N+N1-NN+1)*(N+N1+NN+1))
            A=DFLOAT(4*NN*NN)/A
            A=A*DFLOAT((2*NN+1)*(2*NN-1))
            A=DSQRT(A)
            B=0.5D0*DFLOAT(M-M1)
            D=0D0
            IF(NN.EQ.1) GO TO 5
            B=DFLOAT(2*NN*(NN-1))
            B=DFLOAT((2*M-MM)*NN*(NN-1)-MM*N*(N+1)+
     &               MM*N1*(N1+1))/B
            D=DFLOAT(4*(NN-1)*(NN-1))
            D=D*DFLOAT((2*NN-3)*(2*NN-1))
            D=DFLOAT((NN-MM-1)*(NN+MM-1)*(N1-N+NN-1))/D
            D=D*DFLOAT((N-N1+NN-1)*(N+N1-NN+2)*(N+N1+NN))
            D=DSQRT(D)
    5       C=A*(B*C1-D*C2)
            C2=C1
            C1=C
            CU(NN)=C
    7    CONTINUE
         IF (NNF.LE.NNM) GO TO 50
         CALL DIRECT(N,M,N1,M1,NNU,MM,C)
         CD(NNU)=C
         IF (NNU.EQ.NNM+1) GO TO 50
         C2=0D0
         C1=C
         DO 12 NN=NNU-1,NNM+1,-1
            A=DFLOAT((NN-MM+1)*(NN+MM+1)*(N1-N+NN+1))
            A=A*DFLOAT((N-N1+NN+1)*(N+N1-NN)*(N+N1+NN+2))
            A=DFLOAT(4*(NN+1)*(NN+1))/A
            A=A*DFLOAT((2*NN+1)*(2*NN+3))
            A=DSQRT(A)
            B=DFLOAT(2*(NN+2)*(NN+1))
            B=DFLOAT((2*M-MM)*(NN+2)*(NN+1)-MM*N*(N+1)
     &               +MM*N1*(N1+1))/B
            D=DFLOAT(4*(NN+2)*(NN+2))
            D=D*DFLOAT((2*NN+5)*(2*NN+3))
            D=DFLOAT((NN+MM+2)*(NN-MM+2)*(N1-N+NN+2))/D
            D=D*DFLOAT((N-N1+NN+2)*(N+N1-NN-1)*(N+N1+NN+3))
            D=DSQRT(D)
            C=A*(B*C1-D*C2)
            C2=C1
            C1=C
            CD(NN)=C
   12    CONTINUE
   50    DO 9 NN=NNL,NNF
            IF (NN.LE.NNM) GG(MIND,NN+1)=CU(NN)
            IF (NN.GT.NNM) GG(MIND,NN+1)=CD(NN)
c           WRITE (6,*) N,M,N1,M1,NN,MM,GG(MIND,NN+1)
    9    CONTINUE
   90    CONTINUE
  100 CONTINUE
      RETURN
      END
 
C*********************************************************************
 
      SUBROUTINE DIRECT (N,M,N1,M1,NN,MM,C)
      IMPLICIT REAL*8 (A-H,O-Z)
      REAL*8 F(500)
      COMMON /FAC/ F
      C=F(2*N+1)+F(2*N1+1)+F(N+N1+M+M1+1)+F(N+N1-M-M1+1)    
      C=C-F(2*(N+N1)+1)-F(N+M+1)-F(N-M+1)-F(N1+M1+1)-F(N1-M1+1)
      C=DEXP(C)
      RETURN
      END
 
C*********************************************************************
C
C   CALCULATION OF THE CLEBCSH-GORDAN COEFFICIENTS
C   G=(N,M:N1,MM-M/NN,MM)
C   FOR GIVEN N,N1,M,MM, WHERE NN=MAX(/MM/,/N-N1/)
C                               /M/.LE.N
C                               /MM-M/.LE.N1
C                               /MM/.LE.N+N1
 
      SUBROUTINE CCGIN(N,N1,M,MM,G,ERRCODE)
      IMPLICIT REAL*8 (A-H,O-Z)
      REAL*8 F(500),SSIGN(600)
      INTEGER ERRCODE

      COMMON /SS/ SSIGN
      COMMON /FAC/ F
      M1=MM-M
      IF(N.GE.IABS(M).
     &   AND.N1.GE.IABS(M1).
     &   AND.IABS(MM).LE.(N+N1)) GO TO 1
C      PRINT 5001
C      STOP
C 5001 FORMAT(' ERROR IN SUBROUTINE CCGIN')
      ERRCODE=6
      RETURN
    1 IF (IABS(MM).GT.IABS(N-N1)) GO TO 100
      L1=N
      L2=N1
      L3=M
      IF(N1.LE.N) GO TO 50
      K=N
      N=N1
      N1=K
      K=M
      M=M1
      M1=K
   50 N2=N*2
      M2=M*2
      N12=N1*2
      M12=M1*2
      G=SSIGN(N1+M1+1)
     & *DEXP(F(N+M+1)+F(N-M+1)+F(N12+1)+F(N2-N12+2)-F(N2+2)
     &       -F(N1+M1+1)-F(N1-M1+1)-F(N-N1+MM+1)-F(N-N1-MM+1))
      N=L1
      N1=L2
      M=L3
      RETURN
  100 A=1D0
      L1=M
      L2=MM
      IF(MM.GE.0) GO TO 150
      MM=-MM
      M=-M
      M1=-M1
      A=SSIGN(MM+N+N1+1)
  150 G=A*SSIGN(N+M+1)
     &   *DEXP(F(2*MM+2)+F(N+N1-MM+1)+F(N+M+1)+F(N1+M1+1)
     &        -F(N+N1+MM+2)-F(N-N1+MM+1)-F(-N+N1+MM+1)-F(N-M+1)
     &        -F(N1-M1+1))
      M=L1
      MM=L2
      RETURN
      END
C
C********************************************************************
C
      SUBROUTINE VWHAC2(ITYPE,ICALL,R,NTOT,LTOT,MS,MOLD,NM,LM,
     1           CM,CN,CN1,NCD,C,A,B)
      IMPLICIT REAL*8(A-H,O-Z)
      PARAMETER(NOTD=85,NCDIM2=2*NOTD+2)
      COMPLEX*16 C(0:NCD,0:NCD),C1,C2,CT,CM(0:*),A,B,
     1           CN(0:*),CN1(0:*),CT1,XI(0:NCDIM2)
      COMMON/FACTOR/FAC(0:165)
C
C  COMPUTES SCALAR ADDITION COEFFICIENTS FOR AXIAL TRANSLATION
C
C  INPUT: ITYPE: =1: RETURNS J MATRIX
C                =3: RETURNS H MATRIX
C         ICALL: = 0, INITIALIZE AND/OR PERFORM RECURRENCE IN DEGREE
C                     M TO CALCULATE SCALAR COEFFICIENTS (C)
C                = 1, CALCULATE VECTOR COEFFICIENTS A,B FROM SCALAR C
C                     FOR ORDER NM AND LM AND DEGREE M.
C         R: AXIAL DISTANCE
C         NTOT: ORDER N MAX
C         LTOT: ORDER L MAX
C         MS: DEGREE M OF COEFFICIENTS, MS>=0
C         NCD: DIMENSION OF C ARRAY
C  OUTPUT:A,B:  A,B VECTOR COEFFICIENTS: A^MN_ML ETC
C         C: ARRAY OF SCALAR COEFFICIENTS
C
C  WRITTEN 5/5/94
C
C  USAGE:  START WITH MS=0, CALL VWHAC2 WITH ICALL=0: RETURNS
C          SCALAR C ARRAY.  THEN USE ICALL=1 (WITH SAME M) TO CALC
C          A & B VALUES FOR DIFFERENT NM AND LM'S.  NEXT, SET MS=1,
C          ICALL=0, AND CALC C'S.  THEN SET ICALL=1, GET A,B'S, AND SO
C          ON, UNTIL M=NTOT OR LTOT.
C
      IF(ICALL.EQ.1) GOTO 200
      NMIN=MIN(NTOT,LTOT)
      NMAX=MAX(NTOT,LTOT)
      MSA=ABS(MS)
      IF(MSA.LT.MOLD) MOLD=-1
      NT2=NTOT+LTOT+1
C
C  IF M=0, INITIALIZE, IF M>0, USE RECURRENCE FOR PREVIOUS M
C
      DO 110 M=MOLD+1,MSA
         IF(M.EQ.0) THEN
            CALL BESSEL(NT2,R,XI)
            DO 20 L=0,NT2
               IF(ITYPE.EQ.3) THEN
                  CM(L)=((-1)**L)*(L+L+1)*XI(L)/R
               ELSEIF(ITYPE.EQ.1) THEN
                  CM(L)=((-1)**L)*(L+L+1)*REAL(XI(L))/R
               ENDIF
               IF(L.LE.NMAX+1) C(0,L)=CM(L)
               CN(L)=CM(L)
               CN1(L)=0.
   20       CONTINUE
         ELSE
            CT1=CM(M-1)
            DO 40 L=M,NT2-M
               CT=CM(L)
               CM(L)=(M+M-1)*(CT1/REAL(L+L-1)
     1               +CM(L+1)/REAL(L+L+3))
               CT1=CT
               IF(L.LE.NMAX+1) C(M,L)=CM(L)
               CN(L)=CM(L)
               CN1(L)=0.
   40       CONTINUE
         ENDIF
C
C  CALCULATE FOR N=M+1
C
         CN1(M)=CN(M)
         CN(M)=-(M+M+1)*(M+M+1)/REAL(M+M+3)*CN(M+1)
         C(M+1,M)=CN(M)
         DO 60 L=M+1,NT2-M-1
            CN1(L)=CN(L)
            CN(L)=(M+M+1)*((L-M)/REAL(L+L-1)*CN1(L-1)
     1              -(L+M+1)/REAL(L+L+3)*CN(L+1))
            IF(L.LE.NMAX+1) C(M+1,L)=CN(L)
   60    CONTINUE
C
C  RECURRENCE FOR N=M+2,M+3,... NTOT
C
         DO 100 N=M+1,NMIN
            CT=CN1(M)
            CN1(M)=CN(M)
            CN(M)=((N+M)*CT
     1              -(N+N+1)*(M+M+1)/REAL(M+M+3)*CN(M+1))/REAL(N-M+1)
            C(N+1,M)=CN(M)
            DO 80 L=M+1,NT2-N-1
               CT=CN1(L)
               CN1(L)=CN(L)
               CN(L)=((N+M)*CT+(N+N+1)*((L-M)/REAL(L+L-1)
     1                 *CN1(L-1)-(L+M+1)/REAL(L+L+3)*CN(L+1)))
     1                 /REAL(N-M+1)
               IF(L.LE.NMAX+1) C(N+1,L)=CN(L)
   80       CONTINUE
  100    CONTINUE
  110 CONTINUE
C
      MOLD=MSA
      IF(ICALL.EQ.0) RETURN
C
C  COMPUTATION OF VECTOR COEFFICIENTS FROM SCALAR COEFFICIENTS
C
  200 M=ABS(MS)
      IF(MS.GE.0) THEN
         IF(NM.LE.NMIN) THEN
            N=NM
            L=LM
            FTERM=1.
         ELSE
            L=NM
            N=LM
            FTERM=((-1)**(N+L))*(N+N+1)*L*(L+1)*FAC(N-M)*FAC(L+M)
     1                 /DBLE((L+L+1)*N*(N+1))/FAC(N+M)/FAC(L-M)
         ENDIF
      ELSE
         IF(NM.LE.NMIN) THEN
            N=NM
            L=LM
            FTERM=FAC(N-M)/FAC(N+M)*FAC(L+M)/FAC(L-M)
         ELSE
            L=NM
            N=LM
            FTERM=((-1)**(N+L))*(N+N+1)*L*(L+1)
     1                 /DBLE((L+L+1)*N*(N+1))
         ENDIF
      ENDIF
      C1=(L+M+1)*C(N,L+1)/DBLE((L+1)*(L+L+3))
      IF(M.GT.L-1) THEN
         C2=0.
      ELSE
         C2=(L-M)*C(N,L-1)/DBLE(L*(L+L-1))
      ENDIF
      A=(R*(C1+C2)+C(N,L))*FTERM
      B=DCMPLX(0.,1.)*FTERM*R*MS*C(N,L)/DBLE(L*(L+1))
C
      RETURN
      END
C
C******************************************************************
C
      SUBROUTINE BESSEL(N,DS,XI)
      IMPLICIT REAL*8(A-H,O-Z)
      COMPLEX*16 XI(0:*)
C
C COMPUTES RICCATTI-BESSEL FUNCTION XI(0:N)=PSI(N)+I*CHI(N)
C FOR REAL ARGUMENT DS
C
C CHECK IF RECURRENCE IS FORWARD OR BACKWARDS
C
      IF(INT(DS).GE.N) GOTO 40
C
C BACKWARDS RECURRENCE FOR LOG. DERIVATIVE DN1(N)=PSI'(N)/PSI(N)
C
      NS=NINT(DS+4.*(DS**.3333)+17)
      DNS=0.D0
      DO 20 I=NS-1,N,-1
         SN=REAL(I+1)/DS
         DNS=SN-1.D0/(DNS+SN)
   20 CONTINUE
      XI(N)=DNS
      XI(N-1)=N/DS-1.D0/(DNS+N/DS)
      DO 25 I=N-2,0,-1
         SN=REAL(I+1)/DS
         XI(I)=SN-1.D0/(XI(I+1)+SN)
   25 CONTINUE
C
C FORWARD RECURRENCE: PSI(N) COMPUTED FROM DN1(N)
C
      CHI0=-COS(DS)
      PSI=SIN(DS)
      CHI1=CHI0/DS-PSI
      XI(0)=DCMPLX(PSI,CHI0)
      DO 30 I=1,N
         CHI2=REAL(I+I+1)/DS*CHI1-CHI0
         PSI=PSI/(REAL(I)/DS+XI(I))
         XI(I)=DCMPLX(PSI,CHI1)
         CHI0=CHI1
         CHI1=CHI2
   30 CONTINUE
      RETURN
C
C FORWARD RECURRENCE: APPLICABLE ONLY IF DS > N
C
   40 CHI0=-COS(DS)
      PSI0=SIN(DS)
      CHI1=CHI0/DS-PSI0
      PSI1=PSI0/DS+CHI0
      XI(0)=DCMPLX(PSI0,CHI0)
      DO 50 I=1,N
         SN=REAL(I+I+1)/DS
         CHI2=SN*CHI1-CHI0
         PSI2=SN*PSI1-PSI0
         XI(I)=DCMPLX(PSI1,CHI1)
         CHI0=CHI1
         CHI1=CHI2
         PSI0=PSI1
         PSI1=PSI2
   50 CONTINUE
      RETURN
      END
C
C*****************************************************************
C
      SUBROUTINE MIE1(X,SN,SK,NSTOP,QEXT,QSCA,AN,CN,IRAY)
      IMPLICIT REAL*8 (A-H,O-Z)
      PARAMETER(NOD=60,NOMAX2=2*NOD)
      COMPLEX*16 Y,RI,XIP,PCP,DA,DB,PC(0:NOMAX2),XI(0:NOMAX2),
     1 AN(*),NA,NB
      REAL*8 CN(*)
C
      RI=CMPLX(SN,SK)
      Y=X*RI
      CALL PSIC(NSTOP,Y,PC)
      CALL BESSEL(NSTOP,X,XI)
      QSCA=0.0
      QEXT=0.0
      DO 300 N=1,NSTOP
         N1=N+N-1
         N2=N+N
         PRN=REAL(XI(N))
         PCP=PC(N-1)-N*PC(N)/Y
         XIP=XI(N-1)-N*XI(N)/X
         PRP=REAL(XIP)
         DA=RI*XIP*PC(N)-XI(N)*PCP
         DB=RI*XI(N)*PCP-XIP*PC(N)
         NA=RI*PRP*PC(N)-PRN*PCP
         NB=RI*PRN*PCP-PRP*PC(N)
         AN(N1)=NA/DA
         AN(N2)=NB/DB
         IF(NA.EQ.0.) THEN
            DN1=0.
         ELSE
            DN1=1./NA/DCONJG(NA)
         ENDIF
         IF(NB.EQ.0.) THEN
            CN1=0.
         ELSE
            CN1=1./NB/DCONJG(NB)
         ENDIF
         NA=DCMPLX(0.,1.)*PCP*DCONJG(PC(N))
         CN(N1)=NA*DN1*DCONJG(RI)
         CN(N2)=NA*CN1*RI
C        CN(N1)=DCMPLX(0.D0,1.D0)*RI/NA
C        CN(N2)=-DCMPLX(0.D0,1.D0)*RI/NB
         QSCA=QSCA+(N+N+1)*(CDABS(AN(N1))*CDABS(AN(N1))
     1        +CDABS(AN(N2))*CDABS(AN(N2)))
         QEXT=QEXT+(N+N+1)*REAL(AN(N1)+AN(N2))
  300 CONTINUE
      QSCA=2./X/X*QSCA
      QEXT=2./X/X*QEXT
      IF((NSTOP.EQ.1).AND.(IRAY.GT.0)) THEN
         AN(1)=-DCMPLX(0.,1.)*2.*X*X*X*(RI-1.)*(RI+1.)/(RI*RI+2.)/3.
         IF(IRAY.EQ.2) AN(1)=AN(1)/(1.+AN(1))
      ENDIF
      RETURN
      END
C
C********************************************************************
C
      SUBROUTINE PSIC(N,DS,PSI)
      COMPLEX*16 DS,PSI(0:*),SN,PSINS
C
      NS=NINT(CdABS(DS)+4.*(CdABS(DS)**.3333)+17)
      PSINS=(0.D0,0.D0)
      DO 20 I=NS-1,N,-1
         SN=REAL(I+1)/DS
         PSINS=SN-1.D0/(PSINS+SN)
   20 CONTINUE
      PSI(N)=PSINS
      PSI(N-1)=N/DS-1.D0/(PSINS+N/DS)
      DO 25 I=N-2,1,-1
         SN=REAL(I+1)/DS
         PSI(I)=SN-1.D0/(PSI(I+1)+SN)
   25 CONTINUE
      PSINS=CDSIN(DS)
      PSI(0)=PSINS
      DO 30 I=1,N
         PSINS=PSINS/(REAL(I)/DS+PSI(I))
         PSI(I)=PSINS
   30 CONTINUE
      RETURN
      END
C
C********************************************************************
C
      SUBROUTINE GJ2(A,N1,N,NP)
      IMPLICIT REAL*8(A-H,O-Z)
      PARAMETER(NOD=60,NMAX=2*NOD)
      COMPLEX*16 A(NP,NP),AT(NMAX)
C
C GAUSS ELIMINATION OF A 2-SPHERE INTERACTION MATRIX, IN THE FORM
C
C        I  A^12
C  A =
C       A^21 I
C
C
C  CALC T^11 = [I-A^12 A^21]^-1
C
      DO I=1,N1
         DO J=1,N1
            DO K=N1+1,N
               A(I,J)=A(I,J)-A(I,K)*A(K,J)
            ENDDO
         ENDDO
      ENDDO
      CALL GAUSSJ(A,N1,NP)
C
C CALC T^21 = -A^21 T^11
C
      DO I=N1+1,N
         DO J=1,N1
            AT(J)=A(I,J)
            A(I,J)=0.
         ENDDO
         DO J=1,N1
            DO K=1,N1
               A(I,J)=A(I,J)-AT(K)*A(K,J)
            ENDDO
         ENDDO
      ENDDO
C
C  CALC T^22 = I-T^21 A^12
C
      DO I=N1+1,N
         DO J=N1+1,N
            DO K=1,N1
               A(I,J)=A(I,J)-A(I,K)*A(K,J)
            ENDDO
         ENDDO
      ENDDO
C
C CALC T^12 = - T^11 A^12
C
      DO J=N1+1,N
         DO I=1,N1
            AT(I)=A(I,J)
            A(I,J)=0.
         ENDDO
         DO I=1,N1
            DO K=1,N1
               A(I,J)=A(I,J)-A(I,K)*AT(K)
            ENDDO
         ENDDO
      ENDDO
      RETURN
      END
C
C*******************************************************************
C
      SUBROUTINE GAUSSJ(A,N,NP)
      IMPLICIT REAL*8(A-H,O-Z)
      PARAMETER (NOD=60,NMAX=2*NOD)
      COMPLEX*16 A(NP,NP),DUM,PIVINV
      DIMENSION IPIV(NMAX),INDXR(NMAX),INDXC(NMAX)
      DO 11 J=1,N
        IPIV(J)=0
11    CONTINUE
      DO 22 I=1,N
        BIG=0.
        DO 13 J=1,N
          IF(IPIV(J).NE.1)THEN
            DO 12 K=1,N
              IF (IPIV(K).EQ.0) THEN
                IF (CDABS(A(J,K)).GE.BIG)THEN
                  BIG=CDABS(A(J,K))
                  IROW=J
                  ICOL=K
                ENDIF
              ELSE IF (IPIV(K).GT.1) THEN
                PAUSE 'Singular matrix'
              ENDIF
12          CONTINUE
          ENDIF
13      CONTINUE
        IPIV(ICOL)=IPIV(ICOL)+1
        IF (IROW.NE.ICOL) THEN
          DO 14 L=1,N
            DUM=A(IROW,L)
            A(IROW,L)=A(ICOL,L)
            A(ICOL,L)=DUM
14        CONTINUE
        ENDIF
        INDXR(I)=IROW
        INDXC(I)=ICOL
        IF (A(ICOL,ICOL).EQ.0.) PAUSE 'Singular matrix.'
        PIVINV=1./A(ICOL,ICOL)
        A(ICOL,ICOL)=1.
        DO 16 L=1,N
          A(ICOL,L)=A(ICOL,L)*PIVINV
16      CONTINUE
        DO 21 LL=1,N
          IF(LL.NE.ICOL)THEN
            DUM=A(LL,ICOL)
            A(LL,ICOL)=0.
            DO 18 L=1,N
              A(LL,L)=A(LL,L)-A(ICOL,L)*DUM
18          CONTINUE
          ENDIF
21      CONTINUE
22    CONTINUE
      DO 24 L=N,1,-1
        IF(INDXR(L).NE.INDXC(L))THEN
          DO 23 K=1,N
            DUM=A(K,INDXR(L))
            A(K,INDXR(L))=A(K,INDXC(L))
            A(K,INDXC(L))=DUM
23        CONTINUE
        ENDIF
24    CONTINUE
      RETURN
      END
